plugins {
    id("buildlogic.kotlin")
    id("buildlogic.kotlinx-serialization-json")
    application
}

dependencies {
    implementation(libs.kaml)
}

application {
    mainClass = "app.MainKt"
}

val generatedDir = rootDir.resolve("data/logseq/pages/generated")

val cleanGenerated by tasks.registering(Delete::class) {
    delete(generatedDir)
}

tasks.named<JavaExec>("run") {
    dependsOn(cleanGenerated)
    outputs.dir(generatedDir)
}

tasks.clean {
    dependsOn(cleanGenerated)
}

val extractDescriptions by tasks.registering(JavaExec::class) {
    group = "application"
    description = "Extract descriptions from OpenAPI specs to markdown files"
    classpath = sourceSets.main.get().runtimeClasspath
    mainClass = "app.ExtractKt"
}
